/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
def MAPS_PROPERTY_KEY = "MAPS_API_KEY"

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.google.accompanist.sample"
        minSdkVersion 21
        targetSdkVersion 31

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Add your Google Maps API key to local.properties to use the maps samples.
        // This could normally be done with the Google Maps gradle plugin
        // but handling manually to make sure sample build doesn't fail without
        // a Google Maps key.
        def maps_key = ""
        if (properties.containsKey(MAPS_PROPERTY_KEY)) {
            maps_key = properties.getProperty(MAPS_PROPERTY_KEY)
        }
        manifestPlaceholders[MAPS_PROPERTY_KEY] = maps_key
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }
}

dependencies {
    implementation project(':drawablepainter')
    implementation project(':insets')
    implementation project(':insets-ui')
    implementation project(':navigation-animation')
    implementation project(':navigation-material')
    implementation project(':pager')
    implementation project(':pager-indicators')
    implementation project(':permissions')
    implementation project(':placeholder')
    implementation project(':placeholder-material')
    implementation project(':flowlayout')
    implementation project(':systemuicontroller')
    implementation project(':swiperefresh')
    implementation project(':maps')

    implementation libs.androidx.appcompat

    implementation libs.coil.compose
    implementation libs.coil.gif

    implementation libs.compose.material.material
    implementation libs.compose.material.iconsext
    implementation libs.compose.foundation.layout
    implementation libs.compose.ui.tooling
    implementation libs.compose.ui.util

    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.activity.compose

    implementation libs.androidx.core
    implementation libs.androidx.fragment
    implementation libs.androidx.lifecycle.runtime

    implementation libs.kotlin.stdlib

    lintChecks(project(":permissions-lint"))
}
