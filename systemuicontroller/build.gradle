/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'me.tylerbwong.gradle.metalava'
}

kotlin {
    explicitApi()
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        // targetSdkVersion has no effect for libraries. This is only used for the test APK
        targetSdkVersion 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false

        // Not much we can do about unsafe lint rules from libraries
        disable "UnsafeExperimentalUsageError", "UnsafeExperimentalUsageWarning"
    }

    packagingOptions {
        // Some of the META-INF files conflict with coroutines-test. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled true
    }
}

dependencies {
    implementation libs.androidx.core
    implementation libs.compose.ui.ui

    // ======================
    // Test dependencies
    // ======================

    androidTestImplementation libs.junit
    androidTestImplementation libs.truth

    androidTestImplementation libs.compose.ui.test.junit4
    androidTestImplementation libs.compose.ui.test.manifest
    androidTestImplementation libs.compose.ui.ui
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
}

metalava {
    filename = "api/current.api"
    reportLintsAsErrors = true
}

apply plugin: "com.vanniktech.maven.publish"
