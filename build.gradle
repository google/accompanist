/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import dev.chrisbanes.accompanist.buildsrc.Libs
import dev.chrisbanes.accompanist.buildsrc.Versions

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin

        classpath Libs.gradleMavenPublishPlugin
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.3.0"
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()

        if (Libs.AndroidX.Compose.version.endsWith("SNAPSHOT")) {
            maven {
                url "https://androidx.dev/snapshots/builds/${Libs.AndroidX.Compose.snapshot}/artifacts/ui/repository/"
            }
        }
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.txt')
        }

        groovyGradle {
            target '**/*.gradle'
            greclipse().configFile(project.rootProject.file('spotless/greclipse.properties'))
            licenseHeaderFile project.rootProject.file('spotless/copyright.txt'),
                              '(buildscript|apply|import)'
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            // Set JVM target to 1.8
            jvmTarget = "1.8"

            // Allow use of @OptIn
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"

            // Compose is now based on the Kotlin 1.4 compiler, but we need to use the 1.3.x Kotlin
            // library due to library compatibility, etc. Therefore we explicit set our apiVersion
            // to 1.3 to fix any warnings. Binary dependencies (such as Compose) can continue to
            // use 1.4 if built with that library.
            // TODO: remove this once we move to Kotlin 1.4
            apiVersion = "1.3"

            // Don't require parens on fun type annotations e.g. `@Composable~()~ () -> Unit`
            // TODO: Remove when we move to Kotlin 1.4
            freeCompilerArgs += "-XXLanguage:+NonParenthesizedAnnotationsOnFunctionalTypes"
        }
    }

    // Read in the signing.properties file if it is exists
    def signingPropsFile = rootProject.file('release/signing.properties')
    if (signingPropsFile.exists()) {
        def localProperties = new Properties()
        signingPropsFile.withInputStream { is -> localProperties.load(is) }
        localProperties.each { prop ->
            if (prop.key == "signing.secretKeyRingFile") {
                // If this is the key ring, treat it as a relative path
                project.ext.set(prop.key, rootProject.file(prop.value).absolutePath)
            } else {
                project.ext.set(prop.key, prop.value)
            }
        }
    }
}
