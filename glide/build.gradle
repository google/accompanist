/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import dev.chrisbanes.accompanist.buildsrc.Libs

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false

        // We turn off the Compose feature for now, due to using Compose SNAPSHOTs where the
        // Compose compiler artifact has changed artifact group/name.
        // Instead we add the compiler manually below in the dependencies {}. Also see
        // the root build.gradle for info on how to apply the plugin.
        // TODO: remove this once we upgrade to an AGP version which uses the new artifact details

        // compose true
    }

    composeOptions {
        kotlinCompilerVersion Libs.Kotlin.version
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

afterEvaluate {
    tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
        outputDirectory.set(rootProject.file('docs/api'))
    }
}

dependencies {
    api project(':imageloading-core')
    api Libs.glide

    // Temporary workaround (see above)
    kotlinCompilerPlugin Libs.AndroidX.Compose.compiler

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.foundation

    implementation Libs.Kotlin.stdlib
    implementation Libs.Coroutines.android

    // ======================
    // Test dependencies
    // ======================

    androidTestImplementation project(':imageloading-testutils')

    androidTestImplementation Libs.junit
    androidTestImplementation Libs.truth

    androidTestImplementation Libs.AndroidX.Compose.test
    androidTestImplementation Libs.AndroidX.Compose.ui
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.runner
}

apply plugin: "com.vanniktech.maven.publish"
