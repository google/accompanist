/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

kotlin {
    explicitApi()
}

android {
    namespace "com.google.accompanist.permissions"

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        // targetSdkVersion has no effect for libraries. This is only used for the test APK
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        managedDevices {
            devices {
                [28, 29, 31, 32].each { api ->
                    create("api$api", ManagedVirtualDevice) {
                        device = "Pixel 6"
                        apiLevel = api
                        systemImageSource = "google"
                    }
                }
            }
        }
    }

    packagingOptions {
        // Exclude license files to enable our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation libs.androidx.activity.compose
    implementation libs.compose.foundation.foundation
    implementation libs.kotlin.coroutines.android
    implementation libs.napier

    lintChecks(project(":permissions-lint"))
    lintPublish(project(":permissions-lint"))

    // ======================
    // Test dependencies
    // ======================

    androidTestUtil libs.androidx.test.orchestrator

    androidTestImplementation project(':internal-testutils')
    androidTestImplementation libs.androidx.activity.compose
    androidTestImplementation libs.compose.material.material

    androidTestImplementation libs.junit
    androidTestImplementation libs.truth

    androidTestImplementation libs.compose.ui.test.junit4
    androidTestImplementation libs.compose.ui.test.manifest
    androidTestImplementation libs.compose.foundation.foundation
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.uiAutomator
}

apply plugin: "com.vanniktech.maven.publish"
