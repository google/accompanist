/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import dev.chrisbanes.accompanist.buildsrc.Libs

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    composeOptions {
        kotlinCompilerVersion Libs.Kotlin.version
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false
    }

    packagingOptions {
        def e = excludes
        // AGP 4.1.0-alpha06 added an exclude which breaks Kotlin libraries. Remove the exclude
        // until we upgrade to an AGP version with a fix
        e.remove("/META-INF/*.kotlin_module")
        excludes = e

        // Some of the Coil META-INF files conflict with coroutines-test. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

afterEvaluate {
    tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
        outputDirectory.set(rootProject.file('docs/api'))
    }
}

dependencies {
    api Libs.coil

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.foundation

    implementation Libs.Kotlin.stdlib
    implementation Libs.Coroutines.android

    androidTestImplementation Libs.junit
    androidTestImplementation Libs.truth
    androidTestImplementation Libs.mockk

    androidTestImplementation Libs.Coroutines.test

    androidTestImplementation Libs.AndroidX.Compose.test
    androidTestImplementation Libs.AndroidX.Compose.ui
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.runner
}

apply plugin: "com.vanniktech.maven.publish"
